 {recursive}FTPUpload(from to);c;files;file;name;txt;z;pass;tn;options
 :If 0=⎕NC'recursive' ⋄ recursive←0 ⋄ :EndIf

 :If 0=≢pass←2 ⎕NQ'.' 'GetEnvironment' 'OWUM_FTP_PASS'
     'OWUM_FTP_PASS not found in environment'⎕SIGNAL 11
 :EndIf

 :If 0=⎕NC'#.DRC'
     'DRC'#.⎕CY'conga'
 :EndIf

 ⎕←'Login to ftp.owum.dk'
 c←⎕NEW FtpClient('ftp.owum.dk' 'owum.dk'pass)

 options←(⊂'Wildcard' 1),recursive/(⊂'Recurse' 1)
 files←⊃0(⎕NINFO⍠options)from,'*.*'
 files←('.'≠⊃¨2⊃¨⎕NPARTS¨files)/files ⍝ not .htaccess etc
 ⎕←'Uploading ',from,' to ',to

 :For file :In files
     :If '.'≠⊃name←(≢from)↓file
         ⎕←'  ',name
         tn←file ⎕NTIE 0
         txt←⎕NREAD tn 80 ¯1
         ⎕NUNTIE tn
         :If '.htm'≡¯4↑name
             txt←⎕UCS 'UTF-8' ⎕UCS HTMLHead,(⊃,/Style,¨⊂⎕UCS 13 10),txt,'</div></body>'
         :EndIf
         :If 0≠⊃z←c.PutBin(to,name)txt
             ⎕←'failed: ',⍕z
             ∘∘∘
         :EndIf
     :EndIf
 :EndFor

 ⎕←'Upload complete.'
