 Sail_Move args;grid;row;col;sf;data;id;desc;skip;fra;til;hvem;med;tilm;edit
 (grid row col)←args[1 3 4]
 (id desc skip)←(grid eWG'Values')[row;1 3 5]
 (fra til hvem)←(EVENTS.ID⍳⊂id)⊃¨EVENTS.(Fra Til Deltagere)
 hvem←GetNames hvem

 sf←⍎((-'.'⍳⍨⌽grid)↓grid),'.CLASS' ⍝ This is where we may write
 sf.T.Caption←'Sejlads: ','#',id,': ',desc
 :If 0=≢hvem
    sf.HVEM.Caption←'(ingen tilmeldinger)'
 :Else
    sf.HVEM.Caption←'Tilmeldt: ',2↓⍕', '∘,¨hvem[;1],¨{(0≠≢⍵)/' (',⍵,')'}¨hvem[;2]
 :EndIf

 tilm←sf⍎'TILM'
 med←(⊂ID)∊hvem[;1]
 tilm.KOM.C.Text←(hvem[;1]⍳⊂ID)⊃hvem[;2],⊂''
 tilm.TIL.Caption←(1+med)⊃'Tilmeld' 'Frameld'

 edit←sf⍎'EDIT'
 edit.(FRA TIL SKIP DESC).C.Value←({1 ⎕DT⊂⌽2⊃'-' ⎕VFI ⍵}¨fra til), skip desc
 edit.(FRA TIL SKIP DESC).C.Text←fra til skip desc
