SendMail (mailserver port sender password recipient subject body attachment);smtp;M;⎕USING;A;S;R
⍝ Send message using .NET Core

⍝ mailserver←'mail.dyalog.com'
⍝ sender←'mkrom@dyalog.com' 'Morten Kromberg'
⍝ recipient←'mkrom@dyalog.com'
⍝ password←'…'
⍝ subject←'Hello from Morten'
⍝ body←'This is a test e-mail message'

  :Select ⊃2250⌶⍬
  :Case 1 ⍝ .NET
     ⎕USING←'System.Net,System.Net.Mail' 'System.Net,System.Net.Primitives' ''
  :Case 2 ⍝ .NET Framework
     ⎕USING←'System.Net,System.dll' ''
  :EndSelect

  smtp←⎕NEW Mail.SmtpClient (mailserver port)
  S←⎕NEW Mail.MailAddress (,⊆sender)
  R←⎕NEW Mail.MailAddress (,⊆recipient)
  smtp.Credentials←⎕NEW NetworkCredential ((⊃⊆sender) password)

  M←⎕NEW Mail.MailMessage(S R)
  M.(Subject Body)←subject (∊body)
  M.IsBodyHtml←1
  :If 0≠≢attachment
  :AndIf ⎕NEXISTS attachment
     A←⎕NEW Mail.Attachment (⊂attachment)
     M.Attachments.Add A
  :EndIf
  smtp.Send M
