 Sail_Actions args;sf;tilm;edit;grid;cap;obj;cc;values;data;id;index;fra;til;skip;desc;kom;ctl;i;hvem;new;fratil;html;filter

 ctl←{(1-'.'⍳⍨⌽⍵)↑⍵}obj←⊃args ⍝ Leaf
 sf←(⍎(4+1⍳⍨'CLASS'⍷obj)↑obj).##
 (tilm edit)←sf.CLASS.(TILM EDIT)
 grid←sf.GRID
 cap←obj eWG'Caption'
 new←0

 cc←grid.CurCell
 (values data)←grid.(Values Data)
 id←⊃values[cc[1];1]
 index←⍸EVENTS.ID∊⊂id
 hvem←GetNames index⊃EVENTS.Deltagere

 (fra til skip desc)←edit.(FRA TIL SKIP DESC).C.Value
 (fra til)←{2=+/⍵='-':1 ⎕DT⊂⌽2⊃'-'⎕VFI ⍵ ⋄ ⍵}¨fra til ⍝ /// work around bug in date controls
 kom←tilm.KOM.C.Text

 :If (ctl≢'NEW')∧1≠≢index
     →0⊣DisplayPopup cap('Intern fejl - finder ikke opgaven!')'Error' 'OK'
 :EndIf

 :Select ctl ⍝ See Sail_Make
 :Case 'TIL' ⍝ Til- eller frameld
     :Select cap
     :Case 'Tilmeld'
         hvem⍪←ID kom
     :Case 'Frameld'
         hvem←(~hvem[;1]∊⊂ID)⌿hvem
     :EndSelect
     obj eWS'Caption'(⊃'Tilmeld' 'Frameld'~⊂cap) ⍝ Reverse the caption
     data[data[;1]⍳⊂id;4]←⊂{(0≠⍵)/⍕⍵}≢hvem
     (index⊃EVENTS.Deltagere)←(0≠≢hvem)/⎕JSON,hvem

 :Case 'OPDKOM' ⍝ Opdater kommentar
     :If (≢hvem)<i←hvem[;1]⍳⊂ID
         →0⊣DisplayPopup'Opdater kommentar' 'Du må tilmeldes til sejladsen først.' 'Error' 'OK'
     :EndIf
     hvem[i;2]←⊂kom
     (index⊃EVENTS.Deltagere)←(0≠≢hvem)/⎕JSON,hvem
     sf.CLASS.TILM.Caption←'Tilmeldt: ',2↓⍕', '∘,¨hvem[;1],¨{(0≠≢⍵)/' (',⍵,')'}¨hvem[;2]

 :CaseList 'NEW' 'OPD' ⍝ Create or Update
     new←ctl≡'NEW'
     cap←(new+1)⊃'Opdater sejlads' 'Opret ny sejlads'
     :If fra>til
         →0⊣DisplayPopup cap'Til-dato må ikke være før Fra-dato' 'Error' 'OK'
     :EndIf
     :If 30<til-fra
         →0/⍨1≠DisplayPopup cap('Kan du bekræfte at sejladsen varer i ',(⍕1+til-fra),' dage?')'Query'('YES' 'NO')
     :EndIf

     (fra til)←'DD-MM-YYYY'(1200⌶)fra til
     fratil←(fra,(fra≢til)/' - ',til)
     :If new
         id←⍕1+⌈/0,2⊃⎕VFI⍕EVENTS.ID
         →0/⍨1≠DisplayPopup cap('Bekræft oprettelse af sejlads "',desc,'"')'Query'('YES' 'NO')
         EVENTS.(ID Fra Til Deltagere Beskrivelse Skipper Type)←EVENTS.(ID Fra Til Deltagere Beskrivelse Skipper Type)⍪¨⊂¨id fra til '' desc skip 'Sejlads'
         data⍪←id fratil desc '' skip
         cc[1]←≢data
     :Else
         EVENTS.Beskrivelse[index]←⊂desc
         EVENTS.Fra[index]←⊂fra
         EVENTS.Til[index]←⊂til
         EVENTS.Skipper[index]←⊂skip
         data[data[;1]⍳⊂id;2 3 5]←fratil desc skip
     :EndIf

 :Case 'DEL' ⍝ Delete
    desc←⊃values[cc[1];3]
     →0/⍨1≠DisplayPopup cap('Bekræft slætning af sejlads "',desc,'"')'Query'('YES' 'NO')
     (index≠⍳≢EVENTS.ID)CompressTable'EVENTS'
     data←(~data[;1]∊⊂id)⌿data
     cc[1]←1⌈cc[1]-1

 :Case 'PRT'
     cap←'Sejlads liste'
     html←HtmlTable (values[;1]) (1↓grid.ColTitles) (0 1↓values)
     html←'<b>',cap,' dateret ',(⊃'__da__DD. mmmm YYYY' (1200⌶)1 ⎕DT'J'),'</b><br /><br />',html
     filter←sf.FILTER.F.C.Text
     :If filter∨.≠' '
         html←('Filter: ',filter),'<br \><br \>',html
     :EndIf
     SendText cap html

 :Else
     ...
 :EndSelect

 grid.Data←data
 WriteTable'EVENTS'
 cc[1] doFilterGrid sf
