MakeMain;formsize;bcol;name;error;tb;sf;sfs;R
⍝ Make the main OWUM form

 bcol←219 252 252 ⍝ Pale blue
 :If 0=≢name←⊃(MEDLEMMER.Navn,⊂'')[MEDLEMMER.Mail⍳⊂USERID]
     →ERROR ⊣ error←'Kunne ikke finde din oplysninger.'
 :EndIf

 'F1'eWC'Form' 'Anne Elise''s Medlemsside'(50 50) ('Coord' 'Pixel')('Flex' 'fill')('BCol' bcol)
 'F1.INTRO' eWC 'SubForm' ('Flex' 'column')('CSS' 'align-items: flex-start')
 MakeTop 'F1.INTRO'

 sfs←MakeSFs 'F1' (('Dash' 'Oversigt')('Work' 'Arbejde')('Sail' 'Sejlplan')('Event' 'Møder/Arbejde') ('Dok' 'Dokumenter') ('Div' 'Diverse'),ADMIN/⊂'Admin' 'Admin')

 MakeDashboard sfs.Dash
 MakeWorkGrid sfs.Work
 MakeEventGrid sfs.Event
 MakeSailGrid sfs.Sail
 MakeDokGrid sfs.Dok
 MakeDiverse sfs.Div
 :If ADMIN
     MakeAdminPanel sfs.Admin
 :EndIf
 'Dash' CBGOTO ''
 →0

 ERROR:
 'MB1'eWC'MsgBox' 'OWUM Login'error 'Error' 'OK'('Event'('MsgBtn1' 'MsgBtn2' 'MsgBtn3')1)
 R←eDQ'MB1'
 eEX'MB1' 'F1'

 EWC.closeConnection _EWC.conn
