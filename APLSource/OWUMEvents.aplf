 (arbejde sejladser)←OWUMEvents;TN;antal;a;A;n;i;j;p;min;q;qq;m;ryttere;last;ix;Events;t;today;d;hdrs
 ⍝ Return individual results

 TN←'Aktivitetskalender Anna Elise'NewPage'Website\Aktiviteter.htm'

 Events←⎕FREAD TNEvents 2
 today←1 ⎕DT ⊂3↑⎕TS
 t←(m←Events[;1]≥today)⌿Events[;1 3 4 5] ⍝ Today or later
 t[;1]←'__da__dddd D. mmmm'(1200⌶)t[;1]
 p←⎕FREAD TNEvents (10+⍸m) ⍝ Read participant lists
 d←1↓¨∊¨','∘,¨¨⊣/¨p         ⍝ Participant names
 d←d,¨(0=≢¨d)/¨⊂'(ingen tilmeldte)'
 d←'<span title="'∘,¨d,¨⊂'">'
 t,←d,¨(⍕∘≢¨p),¨⊂'</span>'

 hdrs←'Dato' 'Beskrivelse' 'Tilmeldte'

 :If 0≠≢sejladser←(t[;3]=2)⌿t[;1 2 5 4] ⍝ Sejladser
     wr'<h2>Sejladser</h2>'
     wr'<p>'
     wr'LEFT' HTMLTable '' (hdrs,⊂'Skipper') sejladser
     wr'</p>'
 :End

 :If 0≠≢arbejde←(t[;3]=1)⌿t[;1 2 5] ⍝ Arbejdsdage
     wr'<h2>Arbejdsdage</h2>'
     wr'<p>'
     wr'LEFT'HTMLTable'' hdrs arbejde
     wr'</p>'
 :End

 ⎕NUNTIE TN
